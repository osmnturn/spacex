apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def appName = "Spacex"
def libs = rootProject.ext.libs
def libVersion = rootProject.ext.versions
def sign = rootProject.ext.signingConfigs

android {
    compileSdkVersion libVersion.compileSdkVersion
    defaultConfig {
        applicationId "com.spacex"
        minSdkVersion libVersion.minSdkVersion
        targetSdkVersion libVersion.targetSdkVersion
        versionCode libVersion.versionCode
        versionName libVersion.versionName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 27
        renderscriptSupportModeEnabled true
    }

    signingConfigs {
        release {
            storeFile sign.storeFile
            storePassword sign.storePassword
            keyAlias sign.keyAlias
            keyPassword sign.keyPassword
        }
    }

    buildTypes {
        debug {
//            applicationIdSuffix ".debug"
            versionNameSuffix ".99"
            testCoverageEnabled true
            ext.alwaysUpdateBuildId = false
        }
        release {
            signingConfig signingConfigs.release
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
                proguardFile 'proguard-rules.pro'
            }
        }
    }


    def environment = "environment"
    flavorDimensions environment

    productFlavors {

        dev {
            buildConfigField("String", "BASE_URL", '"https://api.spacexdata.com/v3/"')
            resValue "string", "app_name", "[Dev] $appName"
            dimension environment
        }


        prod {
            buildConfigField("String", "BASE_URL", '"https://api.spacexdata.com/v3/"')
            resValue "string", "app_name", "$appName"
            dimension environment
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    dexOptions {
        javaMaxHeapSize "12g"
        preDexLibraries true
        maxProcessCount 8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":shared")

    //kotlin
    implementation libs.kotlin
    implementation libs.kotlinCore

    //Support
    implementation libs.supportMultidex
    implementation libs.supportDesign
    implementation libs.supportCardview
    implementation libs.supportConstraintLayout

    //Architecture
    implementation libs.archExtensions
    implementation libs.archLiveData
    implementation libs.archViewModel

    //Dagger
    implementation libs.dagger
    implementation libs.daggerAndroid
    implementation libs.daggerSupport
    kapt libs.daggerProcessor
    kapt libs.daggerCompiler

    //Reactivex
    implementation libs.rxjava
    implementation libs.rxjavaAndroid

    //jake wharton
    implementation libs.retrofit
    implementation libs.retrofitConverterGson
    implementation libs.retrofitAdapterRxJava
    implementation libs.okhttp
    implementation libs.okhttpLoggingInterceptor

    //Http Log
    debugImplementation libs.chuck
    releaseImplementation libs.chuckNoOp

    //Glide
    implementation libs.glide
    implementation libs.glideOkhttpIntegration
    kapt libs.glideCompiler
    implementation libs.timber

    //Parcel
    implementation libs.parcelerApi
    kapt libs.parceler


    implementation libs.gson
    implementation libs.hawk
    implementation libs.calligraphy
    implementation libs.eventbus
    implementation libs.topSnackBar

    //test
    testImplementation libs.junit

    //android test
    androidTestImplementation libs.supportRunner
    androidTestImplementation libs.espressoCore

}
repositories {
    mavenCentral()
}